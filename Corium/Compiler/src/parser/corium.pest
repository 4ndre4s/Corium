CompilationUnit = {
    SOI ~ Module ~ (GlobalStatement | StatementSeparator)* ~ EOI
}

GlobalStatement = {
    Function |
    NativeFunction
}

Function = {
     FunctionSignature ~ " {" ~ NEWLINE ~ Block ~ "}" ~ NEWLINE
}

NativeFunction = {
    "native " ~ FunctionSignature ~ NEWLINE
}

FunctionSignature = {
    "fun " ~ Identifier ~ "(" ~ ParameterList? ~ ")" ~ (" " ~ QualifiedName)?
}

Block = {
     (FunctionStatement | StatementSeparator)*
}

FunctionStatement = {
    LocalVariable |
    ReturnStatement
}

Module = {
    "module " ~ QualifiedName ~ NEWLINE
}

LocalVariable = {
    "let " ~ Identifier ~ (" " ~ QualifiedName)? ~ " = " ~ Expression ~ NEWLINE
}

ReturnStatement = {
    "return" ~ (" " ~ Expression)? ~ NEWLINE
}

ParameterList = {
    (Parameter ~ (", " ~ Parameter)*)
}

Parameter = {
    Identifier ~ " " ~ QualifiedName ~ (" = " ~ Expression)?
}

Expression = {
    Literal |
    Identifier |
    "(" ~ Expression ~ ")" |
    UnaryOperator ~ Expression
}

UnaryOperator = @{
    "+" |
    "-" |
    "!" |
    "~"
}

Literal = {
    FloatLiteral |
    IntLiteral |
    BoolLiteral |
    CharLiteral |
    StringLiteral
}

QualifiedName = {
    Identifier ~ ("." ~ Identifier)*
}

FloatLiteral = @{
    (("_"? ~ ASCII_DIGIT)+ ~ "." ~ ("_"? ~ ASCII_DIGIT)+)
}

IntLiteral = @{
    (("_"? ~ ASCII_DIGIT)+)
}

BoolLiteral = @{
    "true" | "false"
}

CharLiteral = @{
    "'" ~ Char ~ "'"
}

StringLiteral = @{
    "\"" ~ Char* ~ "\""
}

Identifier = @{
    ASCII_ALPHA ~ ASCII_ALPHANUMERIC*
}

Char = @{
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{8})
}

StatementSeparator = _{
    NEWLINE |
    SEPARATOR
}
